apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'kotlin-kapt'

def getProperties = { filePath ->
    Properties properties = new Properties()
    properties.load(project.rootProject.file(filePath).newDataInputStream())
    return properties
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.company.app"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            String propertiesPath = 'config.dev.properties'
            if (project.rootProject.file(propertiesPath).exists()) {
                getProperties(propertiesPath).each { prop ->
                    buildConfigField 'String', prop.key, prop.value
                }
            }
        }

        release {
            String propertiesPath = 'config.prod.properties'
            if (project.rootProject.file(propertiesPath).exists()) {
                getProperties(propertiesPath).each { prop ->
                    buildConfigField 'String', prop.key, prop.value
                }
            }

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

// For use with Parcelize
androidExtensions { experimental = true }

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Common Views
    implementation 'com.google.android.material:material:1.1.0-alpha10'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Architecture Components
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'android.arch.navigation:navigation-fragment-ktx:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha05'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-alpha05'

    // LeakCanary
    implementation 'com.squareup.leakcanary:leakcanary-android:2.0-alpha-2'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'

    // Koin
    implementation 'org.koin:koin-android:2.0.1'
    implementation 'org.koin:koin-android-scope:2.0.1'
    implementation 'org.koin:koin-android-viewmodel:2.0.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.koin:koin-test:2.0.1'
    testImplementation 'android.arch.core:core-testing:1.1.1'

    androidTestImplementation 'org.koin:koin-test:2.0.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
}

preBuild.dependsOn ktlint
preBuild.dependsOn detekt
