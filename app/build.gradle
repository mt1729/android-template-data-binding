apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'androidx.navigation.safeargs'

def getProperties = { filePath ->
    Properties properties = new Properties()
    properties.load(project.rootProject.file(filePath).newDataInputStream())
    return properties
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.company.app"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            String propertiesPath = 'config.dev.properties'
            if (project.rootProject.file(propertiesPath).exists()) {
                getProperties(propertiesPath).each { prop ->
                    buildConfigField 'String', prop.key, prop.value
                }
            }
        }

        release {
            String propertiesPath = 'config.prod.properties'
            if (project.rootProject.file(propertiesPath).exists()) {
                getProperties(propertiesPath).each { prop ->
                    buildConfigField 'String', prop.key, prop.value
                }
            }

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    testOptions {
        unitTests.returnDefaultValues true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // Android Components
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.1'
    implementation 'com.google.android.material:material:1.6.1'

    // Retrofit (HTTP)
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Koin (DI)
    implementation 'io.insert-koin:koin-android:3.2.0'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.insert-koin:koin-test:3.2.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "io.insert-koin:koin-test:3.2.0"

    // TODO: - Jetifier could be disabled (build speed increase), though this dependency is still using support libs. Convert to AndroidX or just skip altogether, since this seems to be abandoned
    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
}
